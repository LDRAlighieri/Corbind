/*
 * Copyright 2019 Vladimir Raupov
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {

    ext {
        versions = [
                'kotlin': '1.3.61',
                'kotlin_coroutines': '1.3.3',
                'android_gradle': '3.5.3',
                'androidx': [
                        'core': '1.1.0',
                        'annotation': '1.1.0',
                        'appcompat': '1.1.0',
                        'drawerlayout': '1.0.0',
                        'leanback': '1.0.0',
                        'navigation': '2.1.0',
                        'recycler_view': '1.1.0',
                        'slidingpane_layout': '1.0.0',
                        'swiperefresh_layout': '1.0.0',
                        'viewpager': '1.0.0',
                        'viewpager2': '1.0.0',
                        'material': '1.1.0-rc01'
                ],
                'ktlint': '0.33.0',
                'dokka': '0.10.0'
        ]

        deps = [
                'kotlin': [
                        'stdlib': [
                                'jdk6': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
                                'jdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
                        ],
                        'coroutines': [
                                'core': "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlin_coroutines}",
                                'jdk8': "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:${versions.kotlin_coroutines}",
                                'android': "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlin_coroutines}"
                        ]
                ],
                'androidx': [
                        'core': "androidx.core:core:${versions.androidx.core}",
                        'annotation': "androidx.annotation:annotation:${versions.androidx.annotation}",
                        'appCompat': "androidx.appcompat:appcompat:${versions.androidx.appcompat}",
                        'drawerLayout': "androidx.drawerlayout:drawerlayout:${versions.androidx.drawerlayout}",
                        'leanback': "androidx.leanback:leanback:${versions.androidx.leanback}",
                        'navigation': "androidx.navigation:navigation-runtime:${versions.androidx.navigation}",
                        'recyclerView': "androidx.recyclerview:recyclerview:${versions.androidx.recycler_view}",
                        'slidingPaneLayout': "androidx.slidingpanelayout:slidingpanelayout:${versions.androidx.slidingpane_layout}",
                        'swipeRefreshLayout': "androidx.swiperefreshlayout:swiperefreshlayout:${versions.androidx.swiperefresh_layout}",
                        'viewPager': "androidx.viewpager:viewpager:${versions.androidx.viewpager}",
                        'viewPager2': "androidx.viewpager2:viewpager2:${versions.androidx.viewpager2}"
                ],
                'material': "com.google.android.material:material:${versions.androidx.material}"
        ]

        buildConfig = [
                'compileSdk': 29,
                'minSdk': 14,
                'targetSdk': 29
        ]
    }

    repositories {
        mavenCentral()
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        // Android gradle
        classpath "com.android.tools.build:gradle:${versions.android_gradle}"

        // Kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"

        // Dokka
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}"
    }

}


plugins {
    id "com.diffplug.gradle.spotless" version "3.27.0"
    id 'com.github.ben-manes.versions' version '0.25.0'
    id "io.gitlab.arturbosch.detekt" version "1.3.1"
}


allprojects {

    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

}


subprojects {

    group = GROUP
    version = VERSION_NAME

    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(versions.ktlint)
            licenseHeaderFile project.rootProject.file('spotless/copyright.kt')
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true

            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += "-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi"
            freeCompilerArgs += "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"
            freeCompilerArgs += "-Xuse-experimental=kotlinx.coroutines.FlowPreview"
        }
    }

}


detekt {
    toolVersion = "1.3.1"

    failFast = false
    buildUponDefaultConfig = true

    config = files("default-detekt-config.yml")
    input = files(
            "corbind/src/main/kotlin",
            "corbind-appcompat/src/main/kotlin",
            "corbind-core/src/main/kotlin",
            "corbind-drawerlayout/src/main/kotlin",
            "corbind-leanback/src/main/kotlin",
            "corbind-material/src/main/kotlin",
            "corbind-navigation/src/main/kotlin",
            "corbind-recyclerview/src/main/kotlin",
            "corbind-slidingpanelayout/src/main/kotlin",
            "corbind-swiperefreshlayout/src/main/kotlin",
            "corbind-viewpager/src/main/kotlin",
            "corbind-viewpager2/src/main/kotlin"
    )
    parallel = true

    reports {
        html.enabled = true
        xml.enabled = false
        txt.enabled = false
    }
}


configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            // Force all Kotlin stdlib artifacts to use the same version.
            if (details.requested.group == 'org.jetbrains.kotlin'
                    && details.requested.name.startsWith('kotlin-stdlib')) {
                details.useVersion versions.kotlin
            }
        }
    }
}


dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
